# Project: adams
# Date: 2014-03-20 13:17:38
# User: fracpete
#
adams.flow.control.Flow
	-annotation
	Pings an arbitrary number of hosts, recording the round-trip time,\ndisplaying it and a screenshot is taken of the plot.\nThe flow also launches a webserver serving a website displaying\nthis screenshot and a browser pointing to this URL.
	-actor
		adams.flow.standalone.CallableActors
			-actor
				adams.flow.sink.SequencePlotter
					-writer
						adams.gui.print.NullWriter
					-paintlet
						adams.gui.visualization.sequence.LinePaintlet
					-overlay-paintlet
						adams.gui.visualization.sequence.CirclePaintlet
					-marker-paintlet
						adams.flow.sink.sequenceplotter.NoMarkers
					-error-paintlet
						adams.flow.sink.sequenceplotter.NoErrorPaintlet
					-color-provider
						adams.gui.visualization.core.DefaultColorProvider
					-overlay-color-provider
						adams.gui.visualization.core.DefaultColorProvider
					-title
					Ping
					-axis-x
						adams.gui.visualization.core.AxisPanelOptions
							-label
							timestamp
							-type
							TIME
							-tick-generator
								adams.gui.visualization.core.axis.FancyTickGenerator
							-nth-value
							2
							-width
							40
							-custom-format
							HH:mm:ss
					-axis-y
						adams.gui.visualization.core.AxisPanelOptions
							-label
							time in ms
							-tick-generator
								adams.gui.visualization.core.axis.FancyTickGenerator
							-nth-value
							2
							-width
							60
							-custom-format
							0.0
					-plot-updater
						adams.flow.sink.sequenceplotter.SimplePlotUpdater
					-post-processor
						adams.flow.sink.sequenceplotter.SimplePruning
							-limit
							@{num_points}
			-actor
				adams.flow.control.SubProcess
					-name
					extract timestamp
					-actor
						adams.flow.transformer.StringSplit
							-expression
							\n
					-actor
						adams.flow.transformer.StringMatcher
							-regexp
							.*bytes from.*
					-actor
						adams.flow.transformer.ArrayToSequence
					-actor
						adams.flow.transformer.StringReplace
							-find
							(\\[)([0-9]+\\.[0-9]+)(\\].*)
							-replace
							$2
							-type
							ALL
					-actor
						adams.flow.transformer.Convert
							-conversion
								adams.data.conversion.StringToDouble
			-actor
				adams.flow.control.SubProcess
					-name
					extract time
					-actor
						adams.flow.transformer.StringSplit
							-expression
							\n
					-actor
						adams.flow.transformer.StringMatcher
							-regexp
							.*bytes from.*
					-actor
						adams.flow.transformer.ArrayToSequence
					-actor
						adams.flow.transformer.StringReplace
							-find
							(.*)([0-9]+\\.[0-9]+)( ms)
							-replace
							$2
							-type
							ALL
					-actor
						adams.flow.transformer.Convert
							-conversion
								adams.data.conversion.StringToDouble
	-actor
		adams.flow.standalone.SetVariable
			-name
			ping interval
			-var-name
			wait
			-var-value
			5000
	-actor
		adams.flow.standalone.SetVariable
			-name
			screenshot file
			-var-name
			screenshot
			-var-value
			${EXAMPLE_FLOWS}/www/images/ping_monitor.png
	-actor
		adams.flow.standalone.SetVariable
			-name
			webserver port
			-var-name
			port
			-var-value
			8081
	-actor
		adams.flow.standalone.WebServer
			-name
			start WebServer
			-port
			@{port}
			-handler
				adams.flow.standalone.webserver.ResourceHandler
					-document-root
					${EXAMPLE_FLOWS}/www
					-welcome-file
					ping_monitor.html
			-handler
				adams.flow.standalone.webserver.DefaultHandler
	-actor
		adams.flow.source.Start
	-actor
		adams.flow.control.Trigger
			-name
			calc num datapoints
			-tee
				adams.flow.source.Variable
					-var-name
					wait
			-tee
				adams.flow.transformer.Convert
					-conversion
						adams.data.conversion.StringToDouble
			-tee
				adams.flow.transformer.MathExpression
					-expression
					floor(24 * 60 * 60 * 1000 / X)
			-tee
				adams.flow.transformer.Convert
					-name
					Convert-1
					-conversion
						adams.data.conversion.DoubleToInt
			-tee
				adams.flow.transformer.SetVariable
					-var-name
					num_points
	-actor
		adams.flow.control.Trigger
			-name
			create URL
			-tee
				adams.flow.source.StringConstants
					-string
					http://localhost:
			-tee
				adams.flow.transformer.StringInsert
					-after
					true
					-value
					@{port}/
					-variable
					true
			-tee
				adams.flow.transformer.SetVariable
					-var-name
					url
	-actor
		adams.flow.control.WhileLoop
			-condition
				adams.flow.condition.bool.Expression
			-loop
				adams.flow.source.Start
			-loop
				adams.flow.control.Trigger
					-name
					ping
					-tee
						adams.flow.source.StringConstants
							-string
							google.com
							-string
							yahoo.com
							-string
							wikipedia.org
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							host
					-tee
						adams.flow.control.Tee
							-name
							ping hosts
							-tee
								adams.flow.control.LoadBalancer
									-load
										adams.flow.source.Exec
											-cmd
											ping -c 1 -D -n @{host}
											-variable
											true
											-conversion
												adams.data.conversion.StringToString
									-load
										adams.flow.control.Tee
											-name
											extract timestamp and time
											-tee
												adams.flow.control.Branch
													-branch
														adams.flow.control.SubProcess
															-name
															extract timestamp
															-actor
																adams.flow.transformer.StringSplit
																	-expression
																	\n
															-actor
																adams.flow.transformer.StringMatcher
																	-regexp
																	.*bytes from.*
															-actor
																adams.flow.transformer.ArrayToSequence
															-actor
																adams.flow.transformer.StringReplace
																	-find
																	(\\[)([0-9]+\\.[0-9]+)(\\].*)
																	-replace
																	$2
																	-type
																	ALL
															-actor
																adams.flow.transformer.SetVariable
																	-var-name
																	timestamp
													-branch
														adams.flow.control.SubProcess
															-name
															extract time
															-actor
																adams.flow.transformer.StringSplit
																	-expression
																	\n
															-actor
																adams.flow.transformer.StringMatcher
																	-regexp
																	.*bytes from.*
															-actor
																adams.flow.transformer.ArrayToSequence
															-actor
																adams.flow.transformer.StringReplace
																	-find
																	(.*)([0-9]+[\\.0-9]*)( ms)
																	-replace
																	$2
																	-type
																	ALL
															-actor
																adams.flow.transformer.SetVariable
																	-var-name
																	time
									-load
										adams.flow.control.Trigger
											-name
											plot
											-tee
												adams.flow.source.VariablesArray
													-var-name
													timestamp
													-var-name
													time
											-tee
												adams.flow.control.ArrayProcess
													-actor
														adams.flow.transformer.Convert
															-conversion
																adams.data.conversion.StringToDouble
											-tee
												adams.flow.transformer.MakePlotContainer
													-plot-name
													@{host}
											-tee
												adams.flow.sink.CallableSink
													-callable
													SequencePlotter
									-use-local-vars
									true
					-tee
						adams.flow.control.CallableActorScreenshot
							-callable
							SequencePlotter
							-filename-generator
								adams.core.io.SimpleFixedFilenameGenerator
									-name
									@{screenshot}
							-writer
								adams.gui.print.PNGWriter
					-tee
						adams.flow.control.TriggerOnce
							-name
							bring up browser
							-tee
								adams.flow.source.URLSupplier
									-url
									@{url}
							-tee
								adams.flow.sink.Browser
			-loop
				adams.flow.control.Sleep
					-interval
					@{wait}
	-error-handling
	ACTORS_DECIDE_TO_STOP_ON_ERROR
	-flow-execution-listener
		adams.flow.execution.NullListener

