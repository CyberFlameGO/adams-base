# Project: adams
# Date: 2016-02-15 15:24:05
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Lists the number of attributes for all the datasets it finds\\nin the directory that the user selects." -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.source.Start
 adams.flow.control.Trigger -name "select directory"
  adams.flow.source.SelectDirectory -stop-if-canceled true -custom-stop-message "User canceled selection of dataset directory!" -dir-chooser-title "Select dir with datasets"
  adams.flow.transformer.SetVariable -var-name dir
 adams.flow.control.Trigger -name "check datasets"
  adams.flow.source.DirectoryLister -dir @{dir} -list-files true -regexp .*.arff -sorting SORT_BY_NAME
  adams.flow.control.Tee -name "Store filename in @{file}"
   adams.flow.transformer.BaseName -remove-extensions true
   adams.flow.transformer.SetVariable -var-name file
  adams.flow.control.TryCatch -try "adams.flow.control.SubProcess -name try" -catch "adams.flow.control.SubProcess -name catch"
   adams.flow.control.SubProcess -name try
    adams.flow.transformer.WekaFileReader -loader weka.core.converters.ArffLoader
    adams.flow.transformer.WekaClassSelector
    adams.flow.transformer.WekaInstancesInfo -type NUM_ATTRIBUTES
    adams.flow.transformer.Convert -conversion adams.data.conversion.AnyToString
   adams.flow.control.SubProcess -name catch
    adams.flow.transformer.StringReplace -find .* -replace Error
  adams.flow.transformer.StringInsert -position first -value "@{file}: " -variable true
  adams.flow.sink.Display -writer adams.data.io.output.NullWriter
