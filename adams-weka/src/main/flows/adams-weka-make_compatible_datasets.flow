# Project: adams
# Date: 2015-08-03 12:24:16
# User: fracpete
#
adams.flow.control.Flow
	-annotation
	Generates compatible ARFF files from several spreadsheets.
	-actor
		adams.flow.source.Start
	-actor
		adams.flow.control.Trigger
			-name
			load files
			-tee
				adams.flow.source.WekaSelectDataset
					-output-array
					true
					-stop-if-canceled
					true
					-file-chooser-title
					Select datasets
			-tee
				adams.flow.control.Tee
					-name
					num files
					-tee
						adams.flow.transformer.ArrayLength
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							num_files
			-tee
				adams.flow.control.Trigger
					-name
					new length array
					-tee
						adams.flow.source.NewArray
							-array-class
							java.lang.Integer
							-dimensions
							[@{num_files}]
					-tee
						adams.flow.transformer.SetStorageValue
							-storage-name
							lengths
			-tee
				adams.flow.control.Trigger
					-name
					new name array
					-tee
						adams.flow.source.NewArray
							-dimensions
							[@{num_files}]
					-tee
						adams.flow.transformer.SetStorageValue
							-storage-name
							names
			-tee
				adams.flow.transformer.SetVariable
					-var-name
					index
					-var-value
					0
			-tee
				adams.flow.control.ArrayProcess
					-actor
						adams.flow.transformer.IncVariable
							-var-name
							index
					-actor
						adams.flow.control.Tee
							-name
							name
							-tee
								adams.flow.transformer.BaseName
									-remove-extensions
									true
							-tee
								adams.flow.transformer.SetVariable
									-var-name
									name
							-tee
								adams.flow.control.Trigger
									-name
									update array
									-tee
										adams.flow.source.StorageValue
											-storage-name
											names
									-tee
										adams.flow.transformer.SetArrayElement
											-index
											@{index}
											-value
											@{name}
									-tee
										adams.flow.transformer.SetStorageValue
											-storage-name
											names
					-actor
						adams.flow.transformer.WekaFileReader
							-loader
								weka.core.converters.AArffLoader
					-actor
						adams.flow.transformer.Convert
							-conversion
								adams.data.conversion.WekaInstancesToSpreadSheet
									-data-row-type
										adams.data.spreadsheet.DenseDataRow
									-spreadsheet-type
										adams.data.spreadsheet.SpreadSheet
					-actor
						adams.flow.control.Tee
							-name
							num rows
							-tee
								adams.flow.transformer.SpreadSheetInfo
							-tee
								adams.flow.transformer.SetVariable
									-var-name
									length
							-tee
								adams.flow.control.Trigger
									-name
									update array
									-tee
										adams.flow.source.StorageValue
											-storage-name
											lengths
									-tee
										adams.flow.transformer.SetArrayElement
											-index
											@{index}
											-value
											@{length}
									-tee
										adams.flow.transformer.SetStorageValue
											-storage-name
											lengths
			-tee
				adams.flow.transformer.SetStorageValue
					-storage-name
					sheets
	-actor
		adams.flow.control.Trigger
			-name
			select output dir
			-tee
				adams.flow.source.SelectDirectory
					-stop-if-canceled
					true
					-dir-chooser-title
					Select output dir
			-tee
				adams.flow.transformer.SetVariable
					-var-name
					outdir
	-actor
		adams.flow.control.Trigger
			-name
			combine files
			-tee
				adams.flow.source.StorageValue
					-storage-name
					sheets
			-tee
				adams.flow.transformer.ArrayToSequence
			-tee
				adams.flow.transformer.SpreadSheetAppend
					-no-copy
					true
					-storage-name
					combined_sheets
	-actor
		adams.flow.control.Trigger
			-name
			create combined arff
			-tee
				adams.flow.source.StorageValue
					-storage-name
					combined_sheets
			-tee
				adams.flow.transformer.Convert
					-conversion
						adams.data.conversion.SpreadSheetToWekaInstances
			-tee
				adams.flow.transformer.SetStorageValue
					-storage-name
					combined_arff
	-actor
		adams.flow.control.Trigger
			-name
			create separate arff
			-tee
				adams.flow.source.StorageForLoop
					-upper
					@{num_files}
					-variable
					index
					-storage
					combined_arff
			-tee
				adams.flow.control.Trigger
					-name
					get name
					-tee
						adams.flow.source.StorageValue
							-storage-name
							names
					-tee
						adams.flow.transformer.GetArrayElement
							-index
							@{index}
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							name
			-tee
				adams.flow.control.Trigger
					-name
					get length
					-tee
						adams.flow.source.StorageValue
							-storage-name
							lengths
					-tee
						adams.flow.transformer.GetArrayElement
							-index
							@{index}
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							length
			-tee
				adams.flow.control.Trigger
					-name
					create range
					-tee
						adams.flow.source.CombineVariables
							-expression
							1-@{length}
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							range
			-tee
				adams.flow.control.Trigger
					-name
					output file
					-tee
						adams.flow.source.CombineVariables
							-expression
							@{name}.arff
					-tee
						adams.flow.transformer.PrependDir
							-prefix
							@{outdir}
					-tee
						adams.flow.transformer.SetVariable
							-var-name
							outfile
			-tee
				adams.flow.control.Tee
					-name
					save file
					-tee
						adams.flow.control.UpdateProperties
							-property
							filter.instancesIndices
							-variable
							range
							-sub-actor
								adams.flow.transformer.WekaFilter
									-filter
										weka.filters.unsupervised.instance.RemoveRange
											-V
											-R
											first-last
					-tee
						adams.flow.transformer.WekaRenameRelation
							-replace
							@{name}
					-tee
						adams.flow.sink.WekaFileWriter
							-output
							@{outfile}
							-saver
								weka.core.converters.ArffSaver
									-decimal
									6
			-tee
				adams.flow.control.SubProcess
					-name
					update dataset
					-actor
						adams.flow.control.UpdateProperties
							-property
							filter.instancesIndices
							-variable
							range
							-sub-actor
								adams.flow.transformer.WekaFilter
									-filter
										weka.filters.unsupervised.instance.RemoveRange
											-R
											first-last
					-actor
						adams.flow.transformer.SetStorageValue
							-storage-name
							combined_arff
	-flow-execution-listener
		adams.flow.execution.NullListener

