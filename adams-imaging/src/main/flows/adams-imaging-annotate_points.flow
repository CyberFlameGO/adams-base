# Project: adams
# Date: 2021-05-20 13:54:42
# User: fracpete
# Charset: UTF-8
# Modules: adams-core,adams-imaging,adams-json,adams-spreadsheet,adams-xml
#
adams.flow.control.Flow -annotation "Allows annotating points with specific labels on images." -error-handling ACTORS_DECIDE_TO_STOP_ON_ERROR -flow-execution-listener adams.flow.execution.NullListener -flow-restart-manager adams.flow.control.flowrestart.NullManager
 adams.flow.standalone.CallableActors
  adams.flow.sink.ProgressBar -display-type adams.flow.core.displaytype.Default -writer adams.gui.print.NullWriter -max @{max}
 adams.flow.source.Start
 adams.flow.control.Trigger -name "prompt user"
  adams.flow.standalone.SetVariable -name "restore file" -var-name restore -var-value @{flow_filename_long}.props -value-type FILE_FORWARD_SLASHES -expand-value true
  adams.flow.source.EnterManyValues -name Parameters -stop-if-canceled true -value "adams.flow.source.valuedefinition.DirectoryValueDefinition -name dir -display \"Image directory\" -help \"The directory with the images to annotate\" -file-chooser-title \"Image dir\" -use-forward-slashes true" -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name labels -display \"Labels (blank-separated list)\" -help \"The labels to use for the point annotations.\" -default-value \"joint1 joint2\"" -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name autoadvance -display \"Auto advance labels?\" -help \"If enabled, the next label gets automatically selected once one is selected in the image.\" -type BOOLEAN -default-value false" -output-type MAP -restoration-enabled true -restoration-file @{restore}
  adams.flow.transformer.MapToVariables
 adams.flow.control.Trigger -name iterate
  adams.flow.standalone.SetVariable -name reset -var-name count -var-value 0
  adams.flow.source.FileSystemSearch -output-array true -search "adams.flow.source.filesystemsearch.LocalFileSearch -directory @{dir} -regexp .*\\\\.(jpg|jpeg|png|JPG|JPEG|PNG) -sorting SORT_BY_NAME" -use-forward-slashes true
  adams.flow.transformer.SelectArraySubset -stop-if-canceled true -message "Please select images to annotate" -allow-search true
  adams.flow.control.Tee -name "# images"
   adams.flow.transformer.ArrayLength
   adams.flow.transformer.SetVariable -var-name max
  adams.flow.transformer.ArrayToSequence
  adams.flow.control.Tee -name progress
   adams.flow.transformer.IncVariable -var-name count -output-variable-value true
   adams.flow.sink.CallableSink -callable ProgressBar
  adams.flow.control.Tee -name "report file"
   adams.flow.transformer.Convert -conversion "adams.data.conversion.ReplaceFileExtension -extension .report -use-forward-slashes true"
   adams.flow.transformer.SetVariable -var-name report_file
  adams.flow.transformer.ImageFileReader -reader adams.data.io.input.JAIImageReader -load-meta-data true -meta-data-reader adams.data.io.input.DefaultSimpleReportReader
  adams.flow.transformer.ImageObjectAnnotator -annotations-display "adams.gui.visualization.object.annotationsdisplay.DefaultAnnotationsDisplayGenerator -prefix Point." -annotator "adams.gui.visualization.object.annotator.SimplePointAnnotator -auto-advance-labels @{autoadvance}" -label-selector "adams.gui.visualization.object.labelselector.ButtonSelectorGenerator -label @{labels}" -mouse-click adams.gui.visualization.object.mouseclick.ViewObjects -overlay "adams.gui.visualization.object.overlay.SimplePointOverlayFromReport -use-colors-per-type true -type-color-provider adams.gui.visualization.core.DefaultColorProvider -label-format $ -label-font Dialog-PLAIN-20 -label-anchor MIDDLE_RIGHT -label-offset-x 5 -predefined-labels @{labels}" -best-fit true -interaction-logging-filter adams.gui.visualization.image.interactionlogging.Null -allow-using-previous-report true
  adams.flow.transformer.ReportFileWriter -writer adams.data.io.output.DefaultSimpleReportWriter -dir @{dir} -generator "adams.core.io.SimpleFixedFilenameGenerator -name @{report_file}"
 adams.flow.control.CloseCallableDisplay -callable ProgressBar