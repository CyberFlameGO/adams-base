# Project: adams
# Date: 2017-07-12 14:16:28
# User: fracpete
# Charset: UTF-8
adams.flow.control.Flow -annotation "Extracts objects from annotated images \\nand saves them to an output directory." -error-handling ACTORS_DECIDE_TO_STOP_ON_ERROR -flow-execution-listener adams.flow.execution.NullListener
 adams.flow.standalone.CallableActors
  adams.flow.sink.Display -name Info -short-title true -writer adams.data.io.output.NullWriter
 adams.flow.source.Start
 adams.flow.control.Trigger -name "prompt user"
  adams.flow.source.EnterManyValues -stop-if-canceled true -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name image_dir -display \"Dir with annotated images\" -type DIRECTORY_ABSOLUTE -default-value ${CWD}" -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name prefix -display \"Prefix in report for objects\" -default-value Object." -value "adams.flow.source.valuedefinition.DefaultValueDefinition -name output_dir -display \"Output dir for extracted bg images\" -type DIRECTORY_ABSOLUTE -default-value ${CWD}" -output-type MAP
  adams.flow.transformer.MapToVariables
  adams.flow.transformer.SetVariable -name "prefix regexp" -var-name prefix_regexp -var-value @{prefix}.*\\.x -expand-value true
 adams.flow.control.Trigger -name "Select/process files"
  adams.flow.source.SelectFile -stop-if-canceled true -initial-dir @{image_dir} -extension jpg,jpeg,png -absolute true
  adams.flow.control.Tee -name report
   adams.flow.transformer.Convert -conversion "adams.data.conversion.ReplaceFileExtension -extension .report"
   adams.flow.transformer.SetVariable -var-name report
  adams.flow.control.Tee -name filename
   adams.flow.transformer.BaseName -remove-extensions true
   adams.flow.transformer.SetVariable -var-name file
   adams.flow.sink.CallableSink -callable Info
  adams.flow.control.Continue -condition "adams.flow.condition.bool.Not -condition \"adams.flow.condition.bool.FileExists -file @{report} -generator adams.core.io.NullFilenameGenerator\""
  adams.flow.transformer.ImageReader -reader adams.data.io.input.JAIImageReader
  adams.flow.control.Trigger -name "load report"
   adams.flow.source.Variable -var-name report -conversion adams.data.conversion.StringToString
   adams.flow.transformer.ReportFileReader -reader adams.data.io.input.DefaultSimpleReportReader
   adams.flow.transformer.SetStorageValue -storage-name report
  adams.flow.transformer.MergeReport -type STORAGE -storage report -merge REPLACE
  adams.flow.control.Cast -classname adams.data.image.AbstractImageContainer
  adams.flow.transformer.SetStorageValue -storage-name image
  adams.flow.transformer.MatchReportFields -regexp @{prefix_regexp}
  adams.flow.control.Tee -name "object variables"
   adams.flow.transformer.Convert -conversion adams.data.conversion.FieldToString
   adams.flow.transformer.StringReplace -find .*\\.([0-9]+)\\.x -replace $1
   adams.flow.transformer.SetVariable -name index -var-name index
   adams.flow.transformer.SetVariable -name x -var-name field_x -var-value @{prefix}@{index}.x[N] -expand-value true
   adams.flow.transformer.SetVariable -name y -var-name field_y -var-value @{prefix}@{index}.y[N] -expand-value true
   adams.flow.transformer.SetVariable -name width -var-name field_width -var-value @{prefix}@{index}.width[N] -expand-value true
   adams.flow.transformer.SetVariable -name height -var-name field_height -var-value @{prefix}@{index}.height[N] -expand-value true
   adams.flow.transformer.SetVariable -name "output file" -var-name outfile -var-value @{output_dir}/@{file}-@{index}.png -expand-value true
  adams.flow.control.Trigger -name "object parameters"
   adams.flow.source.StorageValue -storage-name report -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.control.Tee -name x
    adams.flow.transformer.GetReportValue -field @{field_x}
    adams.flow.transformer.SetVariable -var-name x
   adams.flow.control.Tee -name y
    adams.flow.transformer.GetReportValue -field @{field_y}
    adams.flow.transformer.SetVariable -var-name y
   adams.flow.control.Tee -name width
    adams.flow.transformer.GetReportValue -field @{field_width}
    adams.flow.transformer.SetVariable -var-name width
   adams.flow.control.Tee -name height
    adams.flow.transformer.GetReportValue -field @{field_height}
    adams.flow.transformer.SetVariable -var-name height
  adams.flow.control.Trigger -name crop
   adams.flow.source.StorageValue -storage-name image -conversion adams.data.conversion.UnknownToUnknown
   adams.flow.transformer.BufferedImageTransformer -transformer "adams.data.image.transformer.Cropping -algorithm \"adams.data.image.transformer.crop.RelativeCrop -x @{x} -y @{y} -width @{width} -height @{height}\""
   adams.flow.sink.ImageWriter -output @{outfile} -writer adams.data.io.output.JAIImageWriter